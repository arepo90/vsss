#include <Arduino.h>
#include <WiFi.h>
#include <esp_now.h>

#define ROBOT_ID 1
#define HEARTBEAT_INVERVAL 500
uint8_t bridgeAddress[] = {0xC0, 0x49, 0xEF, 0xD1, 0x0D, 0xE0};

struct CommandPayload {
    int vx;
    int vy;
    int dtheta;
} __attribute__((packed));

struct HeartbeatPayload {
    uint8_t robot_id;
} __attribute__((packed));

unsigned long prev_time = 0;
esp_now_peer_info_t bridgePeerInfo;

#define M1_PWM 1
#define M1_A 20
#define M1_B 21

#define M2_PWM 2
#define M2_A 10
#define M2_B 9

#define M3_PWM 4
#define M3_A 5
#define M3_B 6

#define M4_PWM 3
#define M4_A 7
#define M4_B 8 

constexpr int M1_CHANNEL = 0;
constexpr int M2_CHANNEL = 1;
constexpr int M3_CHANNEL = 2;
constexpr int M4_CHANNEL = 3;

constexpr int MIN_PWM = 100;
constexpr int MAX_PWM = 250;
constexpr int CORRECTION_FACTOR = 1;

void setMotors(int m1_speed, int m2_speed, int m3_speed, int m4_speed){
    uint32_t set_mask = 0;
    uint32_t clear_mask = 0;
    set_mask |= (m1_speed > 0) ? (1 << M1_A) : (1 << M1_B);
    set_mask |= (m2_speed > 0) ? (1 << M2_A) : (1 << M2_B);
    set_mask |= (m3_speed > 0) ? (1 << M3_A) : (1 << M3_B);
    set_mask |= (m4_speed > 0) ? (1 << M4_A) : (1 << M4_B);
    clear_mask |= (m1_speed > 0) ? (1 << M1_B) : (1 << M1_A);
    clear_mask |= (m2_speed > 0) ? (1 << M2_B) : (1 << M2_A);
    clear_mask |= (m3_speed > 0) ? (1 << M3_B) : (1 << M3_A);
    clear_mask |= (m4_speed > 0) ? (1 << M4_B) : (1 << M4_A);
    ledcWrite(M1_CHANNEL, abs(m1_speed));
    ledcWrite(M2_CHANNEL, abs(m2_speed));
    ledcWrite(M3_CHANNEL, abs(m3_speed));
    ledcWrite(M4_CHANNEL, abs(m4_speed));
    GPIO.out_w1ts.val = set_mask;
    GPIO.out_w1tc.val = clear_mask;
}

void move(int vx, int vy, int dtheta) {
    long turn_correction = dtheta * CORRECTION_FACTOR;  
    long vel[4];
    int pwm[4];
    long max_vel = 0;
    vel[0] = vy - vx + turn_correction;
    vel[1] = vy + vx + turn_correction;
    vel[2] = -vy + vx + turn_correction;
    vel[3] = -vy - vx + turn_correction;
    for(int i = 0; i < 4; i++){
        if(abs(vel[i]) > max_vel){
            max_vel = abs(vel[i]);
        }
    }
    if(max_vel > 0){
        for(int i = 0; i < 4; i++){
            if(max_vel > MAX_PWM){
                vel[i] = (vel[i] * MAX_PWM) / max_vel;
            }
            if(abs(vel[i]) < 10){
                pwm[i] = 0;
            }
            else{
                int sign = (vel[i] > 0) ? 1 : -1;
                pwm[i] = sign * (MIN_PWM + ((abs(vel[i]) - 1) * (MAX_PWM - MIN_PWM)) / (MAX_PWM - 1));
            }
        }
    }
    else{
        for(int i = 0; i < 4; i++){
            pwm[i] = 0;
        }
    }
    setMotors(pwm[0], pwm[1], pwm[2], pwm[3]);
}

void OnDataRecv(const uint8_t * mac, const uint8_t *incomingData, int len) {
    if(len % sizeof(CommandPayload) == 0 && len >= sizeof(CommandPayload) * (ROBOT_ID+1)){
        int offset = ROBOT_ID * sizeof(CommandPayload);
        CommandPayload cmd;
        memcpy(&cmd, &incomingData[offset], sizeof(CommandPayload));
        move(cmd.vx, cmd.vy, cmd.dtheta);
    }
}

void OnDataSent(const uint8_t *mac_addr, esp_now_send_status_t status){
}

void sendPacket() {
    if(millis() - prev_time > HEARTBEAT_INVERVAL){
        HeartbeatPayload hb;
        hb.robot_id = ROBOT_ID;
        esp_now_send(bridgeAddress, (uint8_t *) &hb, sizeof(HeartbeatPayload));
        prev_time = millis();
    }
}

void setPins(){
    pinMode(M1_A, OUTPUT);
    pinMode(M1_B, OUTPUT);
    pinMode(M2_A, OUTPUT);
    pinMode(M2_B, OUTPUT);
    pinMode(M3_A, OUTPUT);
    pinMode(M3_B, OUTPUT);
    pinMode(M4_A, OUTPUT);
    pinMode(M4_B, OUTPUT);
    ledcSetup(M1_CHANNEL, 5000, 8);
    ledcSetup(M2_CHANNEL, 5000, 8);
    ledcSetup(M3_CHANNEL, 5000, 8);
    ledcSetup(M4_CHANNEL, 5000, 8);
    ledcAttachPin(M1_PWM, M1_CHANNEL);
    ledcAttachPin(M2_PWM, M2_CHANNEL);
    ledcAttachPin(M3_PWM, M3_CHANNEL);  
    ledcAttachPin(M4_PWM, M4_CHANNEL);
}

void initEspNow() {
    WiFi.mode(WIFI_STA);
    WiFi.disconnect();
    if(esp_now_init() != ESP_OK){
        Serial.println("Error initializing ESP-NOW");
        while(true) {
            digitalWrite(8, LOW);
            delay(100);
            digitalWrite(8, HIGH);
            delay(100);
        }
    }

    esp_now_register_send_cb(OnDataSent);
    esp_now_register_recv_cb(OnDataRecv);
    memset(&bridgePeerInfo, 0, sizeof(bridgePeerInfo));
    memcpy(bridgePeerInfo.peer_addr, bridgeAddress, 6);
    bridgePeerInfo.channel = 0;  
    bridgePeerInfo.ifidx = WIFI_IF_STA;
    bridgePeerInfo.encrypt = false;
    
    if (esp_now_add_peer(&bridgePeerInfo) != ESP_OK){
        Serial.println("Failed to add bridge peer");
        return;
    }

    Serial.println("ESP-NOW Initialized.");
}

void setup(){
    Serial.begin(115200);
    Serial.println("whatup");
    pinMode(8, OUTPUT);
    initEspNow();
    Serial.end();  
    delay(500);
    setPins();
    setMotors(0, 0, 0, 0);
}

void loop(){
    sendPacket();
}
